apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'signing'
apply from: rootProject.file("gradle/maven_publish.gradle")

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.sourceFiles
    classifier 'sources'
}

task androidJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
    from androidJavadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            artifact sourcesJar
            artifact androidJavadocJar

            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version VERSION_NAME

            pom {
                name = POM_NAME
                packaging = POM_PACKAGING
                description = POM_DESCRIPTION
                url = POM_URL

                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }

                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                        distribution = POM_LICENCE_DIST
                    }
                }

                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url isReleaseBuild() ? getReleaseRepositoryUrl() : getSnapshotRepositoryUrl()
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
    }
}

signing {
    required { isReleaseBuild() }
    sign publishing.publications.mavenAar
}